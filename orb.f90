module orb
  use kind, only : dp
  use data, only : readdata
  use interp, only : locate

  implicit none

  private
  public :: orbpar

contains

!> Interpolate orbital solution to a specific year to
!> find eccentricity, obliquity, and longitude of perihelion from the moving equinox.
!>
!> this should be a drop-in replacement for the GISS_ORBPAR function
!> available on https://data.giss.nasa.gov/modelE/ar5plots/srorbpar.html
!> (last accessed on 2024-12-17)
!> could be easily incorporated into
!> https://github.com/CESM-Development/paleoToolkit/blob/master/PaleoCalAdjust/f90/modules/GISS_orbpar_subs.f90
subroutine orbpar(yearCE,ecc,obl,lpx)
  real(dp), intent(in) :: yearCE
  real(dp), intent(out) :: ecc, obl, lpx

  ! SOME: figure out how to make this not read the data every time for each timestep
  ! maybe pass it optional parameters so if available, can use those instead?
  real(dp), dimension(:), allocatable :: times, eccs, obls, precs, lpxs, climprecs

  real(dp) :: pi
  real(dp) :: yearBP, time_kyr

  integer :: n, ipos
  real(dp) :: frac

  pi = 4.0_dp*datan(1.0_dp)
  ! this reads the file out.dat, generated by snvec
  ! SOME: change from reading plain-text to binary files?

  call readdata(times, eccs, obls, precs, lpxs, climprecs)
  n = size(times)
  ! re-wrap
  lpxs = modulo(lpxs - pi, 2.0_dp*pi)

  ! the DE431 ephimerides used for the ZB18a solution
  ! has t0 = Julian day 2443144.5003725
  ! this is approximately 1977-01-01 at 00:00:32
  ! so 0 model years = 1977 CE
  ! so to convert from CE to model years, subtract 1977
  ! also: model years are negative, but yearCE is positive

  ! this is the model year in kyr for interpolation
  time_kyr = -(yearCE - 1977.0_dp)*1.0e-3_dp
  ! yearBP = (yearCE - 1950.0_dp) ! yearBP is typically positive
  ipos = locate(times,time_kyr)

  ! print nice errors
  if((time_kyr .lt. -100.0e3_dp) .or. (time_kyr .gt. 0.0_dp)) then
     print *, 'ERROR: Orbital solution is provided between 0 and -100 Myr.'
     print *, 'time_kyr = ',time_kyr
     error stop
  end if

  if(ipos == -1) then
     print *, 'ERROR: Interpolation out of bounds :',time_kyr,times(1),times(n)
     error stop
  end if

  ! print nice warning
  if(time_kyr .lt. -58.0e3_dp) then
     print *, 'Caution: For ZB18a, the interval -100 Myr to -58 Myr is unconstrained due to solar system chaos.'
  end if

  frac = ( time_kyr - times(ipos) ) / ( times(ipos+1) - times(ipos) )

  ecc = eccs(ipos) + frac * (eccs(ipos+1) - eccs(ipos))
  obl = obls(ipos) + frac * (obls(ipos+1) - obls(ipos))
  ! prec = linear_interpolation(times,precs,time_kyr)
  lpx = lpxs(ipos) + frac * (lpxs(ipos+1) - lpxs(ipos))
  ! climprec = linear_interpolation(times,climprec,time_kyr)
end subroutine orbpar

end module orb
