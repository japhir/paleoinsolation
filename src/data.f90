!> Read and Write Files for Insolation.
module data
  use kind, only : dp
  implicit none
  private

  public :: readdata
  public :: readbindata
  public :: writedata

contains

!> Read input from out.dat, generated by snvec
subroutine readdata(pt_solution_file, time, ecc, obl, prec, lpx, climprec)
  real(dp), intent(out), allocatable :: time(:), ecc(:), obl(:), prec(:), lpx(:), climprec(:)
  integer :: io, ios, i, n
  character(len=*), intent(in) :: pt_solution_file
  character(len=512) :: msg
  open(newunit=io,file=pt_solution_file, status = "old", action = "read", &
       iostat=ios, iomsg = msg)
  ! check if file exists/contains anything
  if (ios /= 0) then
     print *, trim(msg)
  else
  ! find the number of lines in the file
     n = 0
     do
        read(io,*,iostat=ios)
        if (ios/=0) then
           rewind(io)
           exit
        else
           n=n+1
        end if
     end do
     allocate(time(n),ecc(n),obl(n),prec(n),lpx(n),climprec(n))
     do i=1,n
        read(io,*) time(i), ecc(i), obl(i), prec(i), lpx(i), climprec(i)
     enddo
  end if
  close(io)
end subroutine readdata

subroutine readbindata(pt_solution_file, time, ecc, obl, prec, lpx, climprec)
    real(dp), intent(out), allocatable :: time(:), ecc(:), obl(:), prec(:), lpx(:), climprec(:)
    character(len=*), intent(in) :: pt_solution_file
    ! note that this must be the below Int32 type!
    integer(4) :: n


    ! Read kount as 4-byte integer
    ! Open the binary file in stream mode
    open(unit=10, file=pt_solution_file, access='stream', form='unformatted', status='old', action='read')
    read(10) n
    allocate(time(n), ecc(n), obl(n), prec(n), lpx(n), climprec(n))
    ! Read each array
    read(10) time
    read(10) ecc
    read(10) obl
    read(10) prec
    read(10) lpx
    read(10) climprec
    close(10)

    ! Convert time from days to kyr
    time = time / 365250.0_dp
end subroutine readbindata

!> Write output to ins_file
subroutine writedata(ins_file, time, ecc, obl, prec, lpx, climprec, ins)
  real(dp), intent(in), dimension(:) :: time, ecc, obl, prec, lpx, climprec, ins
  integer :: i,n
  character(len=*) :: ins_file
  n = size(time)
  open (unit=42, file=ins_file, status="replace", action="write")
  do i=1,n
     write(42,*) time(i), ecc(i), obl(i), prec(i), lpx(i), climprec(i), ins(i)
  enddo
  close(42)
end subroutine writedata

end module data
