module orb
  use kind, only : dp
  use data, only : readdata, readbindata
  use interp, only : locate

  implicit none

  private
  public :: orbpar
  public :: orbinterp

contains

!> Interpolate orbital solution to a specific year to
!> find eccentricity, obliquity, and longitude of perihelion from the moving equinox.
!>
!> this should be a drop-in replacement for the GISS_ORBPAR function
!> available on https://data.giss.nasa.gov/modelE/ar5plots/srorbpar.html
!> (last accessed on 2024-12-17)
!> could be easily incorporated into
!> https://github.com/CESM-Development/paleoToolkit/blob/master/PaleoCalAdjust/f90/modules/GISS_orbpar_subs.f90
subroutine orbpar(yearCE,ecc,obl,lpx)
  real(dp), intent(in) :: yearCE
  real(dp), intent(out) :: ecc, obl, lpx

  ! SOME: figure out how to make this not read the data every time for each timestep
  ! maybe pass it optional parameters so if available, can use those instead?
  real(dp), dimension(:), allocatable :: times, eccs, obls, precs, lpxs, climprecs

  real(dp) :: pi
  real(dp) :: yearBP, time_kyr

  integer :: n, ipos
  real(dp) :: frac

  pi = 4.0_dp*datan(1.0_dp)
  ! this reads the output file generated by snvec
  ! SOME: add a flag to choose ZB18a or ZB20a?
!!$  call readdata("dat/PT-ZB18a_1-1.dat", times, eccs, obls, precs, lpxs, climprecs)
  call readbindata("dat/PT-ZB18a_1-1.bin", times, eccs, obls, precs, lpxs, climprecs)
  n = size(times)

  ! the DE431 ephimerides used for the ZB18a and ZB20a solutions
  ! has t0 = Julian day 2443144.5003725 (Folkner et al., 2014)
  ! this is approximately 1977-01-01 at 00:00:32 (web tool conversion)

  ! However, Richard has taken care of this by first converting everything
  ! t0 to J2000.0

  ! this is the model year in kyr for interpolation
  time_kyr = (yearCE - 2000.0_dp)*1.0e-3_dp

  ! print nice errors if time outside orbital solution is specified
  if((time_kyr .lt. -300.0e3_dp) .or. (time_kyr .gt. 0.0_dp)) then
     print *, 'ERROR: Orbital solution is provided between -300 Myr and 0 Myr.'
     print *, 'time_kyr = ',time_kyr
     error stop
  end if

  ! print warning if time is older then well-constrained eccentricity
  if(time_kyr .lt. -58.0e3_dp) then
     print *, 'Caution: For ZB18a, the interval -300 Myr to -58 Myr is unconstrained due to solar system chaos.'
!!$     print *, 'Caution: For ZB20a, the interval -300 Myr to -71 Myr is unconstrained due to solar system chaos.'
  end if

  ! linear interpolation
  ipos = locate(times,time_kyr)
  if(ipos == -1) then
     print *, 'ERROR: Interpolation out of bounds :',time_kyr,times(1),times(n)
     error stop
  end if

  ! linear interpolation
  frac = ( time_kyr - times(ipos) ) / ( times(ipos+1) - times(ipos) )

  ecc = eccs(ipos) + frac * (eccs(ipos+1) - eccs(ipos))
  obl = obls(ipos) + frac * (obls(ipos+1) - obls(ipos))
  ! prec = linear_interpolation(times,precs,time_kyr)
  lpx = lpxs(ipos) + frac * (lpxs(ipos+1) - lpxs(ipos))

  ! re-wrap
  ! important to do this only at the end!
  lpx = modulo(lpx - pi, 2.0_dp*pi)

  ! climprec = linear_interpolation(times,climprec,time_kyr)
end subroutine orbpar

!> Interpolate orbital solution to a specific year to
!> find eccentricity, obliquity, and longitude of perihelion from the moving equinox.
!>
!> same as orbpar, but relies on vectors that have been previously read in to memory.
subroutine orbinterp(yearCE,ecc,obl,lpx,times,eccs,obls,lpxs)
  real(dp), intent(in) :: yearCE
  real(dp), intent(out) :: ecc, obl, lpx
  real(dp), intent(in), dimension(:) :: times, eccs, obls, lpxs

  real(dp) :: pi
  real(dp) :: yearBP, time_kyr

  integer :: n, ipos
  real(dp) :: frac

  pi = 4.0_dp*datan(1.0_dp)
  n = size(times)

  ! the DE431 ephimerides used for the ZB18a and ZB20a solutions
  ! has t0 = Julian day 2443144.5003725 (Folkner et al., 2014)
  ! this is approximately 1977-01-01 at 00:00:32 (web tool conversion)
  ! However, Richard has taken care of this by never using
  ! Julian dates in his t0 ephimerides, he first converted
  ! everything to J2000.0

  ! this is the model year in kyr for interpolation
  time_kyr = (yearCE - 2000.0_dp)*1.0e-3_dp

  ! print nice errors if time outside orbital solution is specified
  if((time_kyr .lt. -300.0e3_dp) .or. (time_kyr .gt. 0.0_dp)) then
     print *, 'ERROR: Orbital solution is provided between -300 Myr and 0 Myr.'
     print *, 'time_kyr = ',time_kyr
     error stop
  end if

  ! print warning if time is older then well-constrained eccentricity
  if(time_kyr .lt. -58.0e3_dp) then
     print *, 'Caution: For ZB18a, the interval -300 Myr to -58 Myr is unconstrained due to solar system chaos.'
!!$     print *, 'Caution: For ZB20a, the interval -300 Myr to -71 Myr is unconstrained due to solar system chaos.'
  end if

  ! linear interpolation
  ipos = locate(times,time_kyr)
  if(ipos == -1) then
     print *, 'ERROR: Interpolation out of bounds :',time_kyr,times(1),times(n)
     error stop
  end if

  ! linear interpolation
  frac = ( time_kyr - times(ipos) ) / ( times(ipos+1) - times(ipos) )

  ecc = eccs(ipos) + frac * (eccs(ipos+1) - eccs(ipos))
  obl = obls(ipos) + frac * (obls(ipos+1) - obls(ipos))
  ! prec = linear_interpolation(times,precs,time_kyr)
  lpx = lpxs(ipos) + frac * (lpxs(ipos+1) - lpxs(ipos))
  ! climprec = linear_interpolation(times,climprec,time_kyr)

  ! re-wrap
  ! important to do this only at the end!
  lpx = modulo(lpx - pi, 2.0_dp*pi)
end subroutine orbinterp


end module orb
